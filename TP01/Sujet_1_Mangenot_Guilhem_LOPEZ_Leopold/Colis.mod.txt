#  GLPK model file created by SUN for RO teaching


###############################  Model ###############################


###############################  Sets  ###############################

set DEMANDES;
set FLUIDES;
set MAGASINS;

################### Variables ###################

var livraison{d in DEMANDES, f in FLUIDES, m in MAGASINS}, >=0;
var livraisonBinaire{d in DEMANDES, m in MAGASINS}, binary;

###################  Constants: Data to load   #########################

param demande{d in DEMANDES, f in FLUIDES}, >=0;
param stocks{f in FLUIDES, m in MAGASINS}, >=0;
param couts{f in FLUIDES, m in MAGASINS}, >=0;
param coutsFixes{m in MAGASINS}, >=0;
param coutsVariables{d in DEMANDES, m in MAGASINS}, >=0;

################### Constraints ###################

s.t. LimitesStocks{f in FLUIDES, m in MAGASINS}:
  sum{d in DEMANDES} livraison[d,f,m] <= stocks[f,m];

s.t. DemandeSatisfaite{d in DEMANDES, f in FLUIDES}:
  demande[d,f] == sum{m in MAGASINS} livraison[d,f,m];

s.t. DefBinaireZero{d in DEMANDES, m in MAGASINS}:
  livraisonBinaire[d,m] <= sum{f in FLUIDES} livraison[d,f,m];
s.t. DefBinaireUn{d in DEMANDES, m in MAGASINS}:
  sum {f in FLUIDES} livraison[d,f,m] <= (sum{f in FLUIDES} stocks[f,m]) * livraisonBinaire[d,m];

###### Objective ######

minimize cout:
		sum{d in DEMANDES} sum{f in FLUIDES} sum{m in MAGASINS} livraison[d,f,m] * couts[f,m]
    + sum{d in DEMANDES} sum{m in MAGASINS} livraisonBinaire[d,m] * coutsFixes[m]
    + sum{d in DEMANDES} sum{f in FLUIDES} sum{m in MAGASINS} livraison[d,f,m] * coutsVariables[d,m];

end;
